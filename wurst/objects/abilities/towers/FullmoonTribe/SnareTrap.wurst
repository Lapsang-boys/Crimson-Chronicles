package SnareTrap

import RegisterEvents
import Assets
import ChannelAbilityPreset
import TimerUtils

import Alexandria
import TowerConstants
import Tooltip

import TrapBase

constant ENSNARE_DURATION = 3.
public constant SNARE_TRAP_COOLDOWN = 5.
constant ENSNARE_FX = Abilities.ensnareTarget
constant TRAP_DEATH_EFFECT = Objects.entBirthTarget


// Legacy definiton
// ChannelAbilityPreset trapCoolDownAbility

// @compiletime function generateTrapOnCooldown()
//     trapCoolDownAbility = new ChannelAbilityPreset(ID_TOWER_UREN_TRAP_COOLDOWN, 1, true)


// ##################################################################
// ###################### Ability objects ###########################
// ##################################################################

@compiletime function generateSetTrap()
    new ChannelAbilityPreset(ID_TOWER_UREN_SET_TRAP, 1, true)
        ..setName("Set trap")
        ..setHotkeyNormal("Q")
        ..setTooltipNormal(1, tooltipCustomBasic("Q", "Set trap", ""))
        ..setTooltipNormalExtended(1, tooltipTextFlavor("Set at loction",
        "Flavor."))
        ..setIconNormal(Icons.bTNEnsnare)

        ..presetTargetTypes(Targettype.POINT)
        ..setAreaofEffect(1, 64)
        ..setManaCost(1, 0)
        ..setHeroAbility(false)

        ..setButtonPositionNormalX(0)
        ..setButtonPositionNormalY(2)

        ..setBaseOrderID(1, SET_TRAP_ORDER)

@compiletime function generateDismantleTrap()
    new ChannelAbilityPreset(ID_TOWER_UREN_DISMANTLE_TRAP, 1, true)
        ..setName("Dismantle trap")
        ..setHotkeyNormal("Q")
        ..setTooltipNormal(1, tooltipCustomBasic("Q", "Dismantle trap", ""))
        ..setTooltipNormalExtended(1, tooltipTextFlavor("Dismantles the currently set trap. Please not that you will not be able to place a new one for 30 seconds.",
        "Flavor."))
        ..setIconNormal(Icons.pASBTNDemolish)

        ..presetTargetTypes(Targettype.NONE)
        ..setManaCost(1, 0)
        ..setHeroAbility(false)

        ..setButtonPositionNormalX(0)
        ..setButtonPositionNormalY(2)

@compiletime function generateTrapOnCooldown()
    new ChannelAbilityPreset(ID_TOWER_UREN_TRAP_COOLDOWN, 1, true)
        ..setName("Set trap")
        ..setHotkeyNormal("Q")
        ..setTooltipNormal(1, tooltipCustomBasic("Q", "Set trap", ""))
        ..setTooltipNormalExtended(1, tooltipTextFlavor("Set at loction",
        "Flavor."))
        ..setIconNormal(Icons.bTNEnsnare)

        ..presetTargetTypes(Targettype.NONE)
        ..setCooldown(1, SNARE_TRAP_COOLDOWN)
        ..setManaCost(1, 0)
        ..setHeroAbility(false)

        ..setButtonPositionNormalX(0)
        ..setButtonPositionNormalY(2)

        ..setBaseOrderID(1, TRAP_COOLDOWN_ORDER)

// ##################################################################
// ###################### Trap object ###############################
// ##################################################################

class SnareTrapDummy extends UnitDefinition
    construct(int id)
        super(id, 'hfoo')

        setName("Snare trap")
        setModelFile("Doodads\\Cinematic\\GlowingRunes\\GlowingRunes4.mdl")
        setScalingValue(0.5)
        setIconGameInterface(Icons.bTNAcorn)
        setUnitSoundSet("")
        setNormalAbilities("Avul")

        setAttacksEnabled(0)
        setSpeedBase(0)
        setMovementType(MovementType.None)
        setCollisionSize(0)
        setFoodCost(0)
        setHitPointsMaximumBase(1)
        setHitPointsRegenerationType("none")
        setManaMaximum(0)

@compiletime function generateSnareTrapDummy()
    new SnareTrapDummy(ID_DUMMY_SNARE_TRAP)

// ##################################################################
// ######################### Ensnare ################################
// ##################################################################

class EnsnareContainer
    unit u
    real ms
    effect e
    construct(unit u, real ms, effect e)
        this.u = u
        this.ms = ms
        this.e = e

    ondestroy
        this.e.destr()

function ensnare(unit u)
    real ms = u.getMoveSpeed()
    effect e = u.addEffect(ENSNARE_FX, "origin")
    u.setMoveSpeed(1.)
    EnsnareContainer ec1 = new EnsnareContainer(u, ms, e)

    timer t = getTimer()
    t.setData(ec1 castTo int)
    t.start(ENSNARE_DURATION, () -> begin
        timer tInner = GetExpiredTimer()
        EnsnareContainer ec2 = tInner.getData() castTo EnsnareContainer
        ec2.u.setMoveSpeed(ec2.ms)
        tInner.release()
        destroy ec2
    end)

// ##################################################################
// ###################### Ability script ############################
// ##################################################################

// Actions when unit walks on trap
function onTrapTrigger()
    trigger trig = GetTriggeringTrigger()
    TrapBase tb = trapMap.get(trig.getHandleId())

    Trapper tr = towerMap.get(tb.caster.getHandleId()) castTo Trapper
    tr.trap = null

    tr.toggleAbility()
    ensnare(GetTriggerUnit())

function setTrapSnare()
    unit caster = GetTriggerUnit()
    vec2 loc = vec2(GetSpellTargetX(), GetSpellTargetY())
    Trapper tr = towerMap.get(caster.getHandleId()) castTo Trapper
    tr.setTrap(loc, null, function onTrapTrigger, TRAP_DEATH_EFFECT)

function disMantleTrapSnare()
    unit caster = GetTriggerUnit()
    Trapper tr = towerMap.get(caster.getHandleId()) castTo Trapper
    tr.disMantleTrap()

@initThird function initSnareTrap()
    registerSpellEffectEvent(ID_TOWER_UREN_SET_TRAP, function setTrapSnare)
    registerSpellEffectEvent(ID_TOWER_UREN_DISMANTLE_TRAP, function disMantleTrapSnare)