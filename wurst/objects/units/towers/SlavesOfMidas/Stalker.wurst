package Stalker

import Assets
import Damage
import UnitObjEditing
import ObjectIds

import TowerDefinition
import TowerConstants
import Tooltip

constant GOLD_COST = 17
constant RANGE = 400
constant ATTACK_SPEED = 1.2
constant DAMAGE_BASE = 6
constant DAMAGE_SIDES_PER_DIE = 12

constant POINT_VALUE = GOLD_COST

class Stalker extends TowerDefinition
    construct(int id)
        super(id)
        // -------------------------------------
        // ========== Model file etc. ==========
        // -------------------------------------
        setIconGameInterface(Icons.bTNArcher)
        setModelFile(Units.archer)
        setTintingColorRed(160)
        setTintingColorGreen(160)
        setTintingColorBlue(160)
        setUnitSoundSet(Soundsets.archer)
        setScalingValue(1.0)
        setGroundTexture("HSMA")

        // ------------------------------------
        // ========== Attack damage ==========
        // ------------------------------------
        setAcquisitionRange(RANGE.toReal())
        setAttack1Range(RANGE)
        setAttack1CooldownTime(ATTACK_SPEED)
        setAttack1DamageBase(DAMAGE_BASE)
        setAttack1DamageSidesperDie(DAMAGE_SIDES_PER_DIE)

        // ------------------------------------
        // ========== Spells ==================
        // ------------------------------------
        setNormalAbilities(commaList(
            ID_SELL_TOWER,
            ID_SONG_OF_RAVENS
        ))

        // ------------------------------------------------
        // ========== Attack modelfile speed etc ==========
        // ------------------------------------------------
        setAttack1ProjectileArt(Abilities.arrowMissile1)
        setAttack1ProjectileHomingEnabled(true)
        setAttack1ProjectileSpeed(1200)
        setAttack1WeaponType(WeaponType.Missile)

        // ----------------------------------------------------------
        // ========== Stats (cost, upgrades, requirements) ==========
        // ----------------------------------------------------------
        setGoldCost(GOLD_COST)
        setPointValue(POINT_VALUE)
        setUpgradesTo(ID_TOWER_KERES.toRawCode())
        setUpgradesUsed("")

        // ---------------------------------
        // ========== Description ==========
        // ---------------------------------
        setButtonPositionX(1)
        setButtonPositionY(0)
        setHotkey("W")
        setName("Stalker")
        setTooltipBasic(tooltipBuild("W", "Stalker"))
        setTooltipExtended(tooltipTowerExtended("Basic tower, good initially and goos for mazing.",
            damageBoundary(DAMAGE_BASE, DAMAGE_SIDES_PER_DIE),
            ATTACK_SPEED,
            RANGE,
            "",
            "It is said that amber eyes are a sign of natural ability for druidic magic but they also provide great vision in moonlight."))

@compiletime function tower()
    new Stalker(ID_TOWER_STALKER)