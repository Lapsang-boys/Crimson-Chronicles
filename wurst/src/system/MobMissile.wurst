package MobMissile

import TimerUtils

import Movement
import TimedEffect

constant ITERATION_TIME = 0.01
constant GRAVITY = -1280.

public class MobMissile
    unit u
    real v0
    real alpha
    real z0
    string impactFx

    real velocityX
    real velocityZ
    angle direction

    real flyTime
    real acceleartionZ = GRAVITY

    construct(unit u, real v0, real alpha, real z0, string impactFx)
        this.u = u
        this.v0 = v0
        this.alpha = Deg2Rad(alpha)
        this.z0 = z0
        this.impactFx = impactFx

        this.direction = u.getFacingAngle()
        this.velocityX = this.v0*Cos(this.alpha)
        this.velocityZ = this.v0*Sin(this.alpha)

        this.flyTime = computeFlyTime()
        this.acceleartionZ *= ITERATION_TIME

    function launch()
        // Make sure landing is not out of bounds
        this.calcLandingSpot()

        this.u.pause()
        // Add storm crow to be able to modify flyheight (...)
        this.u.addAbility('Arav')
        this.u.setFlyHeight(this.z0, 99999)
        this.u.setPathing(false)
        this.u.setInvulnerable(true)

        timer t = getTimer()
        t.setData(this castTo int)
        t.start(ITERATION_TIME, function moveMissile)

    function moveX()
        vec2 newPos = this.u.getPos().polarOffset(this.direction, this.velocityX*ITERATION_TIME)
        this.u.setPos(newPos)

    function moveZ()
        this.updateZveolcity()
        real zMove = this.velocityZ*ITERATION_TIME

        this.u.setFlyHeight(this.u.getFlyHeight() + zMove, 999)

    function updateZveolcity()
        this.velocityZ += this.acceleartionZ

    function computeFlyTime() returns real
        real timeToTop = (this.velocityZ/this.acceleartionZ).abs()
        real topHeight = this.z0 + this.velocityZ*timeToTop + (this.acceleartionZ*timeToTop.squared())/2
        real timeToGround = SquareRoot(2*topHeight/this.acceleartionZ.abs())

        return timeToTop + timeToGround

    function calcLandingSpot()
        vec2 origin = this.u.getPos()

        for i = 0 to 10
            vec2 landingSpot = origin.polarOffset(this.direction, this.velocityX*this.flyTime)

            if validSpot(landingSpot)
                u.setFacing(this.direction)
                return
            else
                this.direction += angle(PI/9)


    function validSpot(vec2 landingSpot) returns bool
        rect pArea = u.getArea()

        vec2 l1 = landingSpot.add(128, 0)
        vec2 l2 = landingSpot.add(0, 128)
        vec2 l3 = landingSpot.add(-128, 0)
        vec2 l4 = landingSpot.add(0, -128)

        if not l1.isInRect(pArea)
            return false
        if not l2.isInRect(pArea)
            return false
        if not l3.isInRect(pArea)
            return false
        if not l4.isInRect(pArea)
            return false
        else
            return true


function moveMissile()
    timer t = GetExpiredTimer()
    MobMissile mm = t.getData() castTo MobMissile

    mm.moveX()
    mm.moveZ()

    if mm.u.getFlyHeight() <= 0.1
        mm.u.setPathing(true)
        mm.u.setInvulnerable(false)
        mm.u.unpause()
        mm.u.removeAbility('Arav')
        mm.u.issuePointOrder("move", mm.u.getNext())
        createTimedEffect(mm.u.getPos(), 1., mm.impactFx)
        t.release()
        return

    t.start(ITERATION_TIME, function moveMissile)
