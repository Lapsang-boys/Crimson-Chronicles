package PlayerData

import LinkedList
import ClosureForGroups
import ErrorHandling
import Levels

import Regions
import Leaderboard
import GameplayConstants

class Remover implements ForGroupCallback
    bool removePlayerUnits
    bool removeBuilder
    player p
    unit builder
    construct(bool removePlayerUnits, bool removeBuilder, player p, unit builder)
        this.removePlayerUnits = removePlayerUnits
        this.removeBuilder = removeBuilder
        this.p = p
        this.builder = builder

    function callback(unit u)
        if not removeBuilder and u.getHandleId() == builder.getHandleId()
            return
        if removePlayerUnits
            u.remove()
        else if not u.getOwner() == p
            u.remove()

public class PlayerData
    int playerNum
    player p
    int levelNum
    int zoneNum
    int life
    int mobsRemoved
    unit builder
    bool levelInProgress
    int difficulty

    construct(int playerNum, int levelNum, int zoneNum, int life)
        this.playerNum = playerNum
        this.p = Player(playerNum)
        this.levelNum = levelNum
        this.zoneNum = zoneNum
        this.life = life
        this.mobsRemoved = 0
        this.levelInProgress = false
        this.difficulty = NORMAL

    function setBuilder(unit builder)
        this.builder = builder

    function cleanse(bool removePlayerUnits, bool removeBuilder)
        Remover rem = new Remover(removePlayerUnits, removeBuilder, this.p, this.builder)
        forUnitsInRect(this.getPlayerRegion(), rem)

    function reset()
        this.levelInProgress = false
        this.zoneNum = 0
        this.life = STARTING_LIFE
        this.mobsRemoved = 0
        this.levelNum = 0
        updateLife(this.p, this.life)

    function getPlayerRegion() returns rect
        return this.getArea().area

    function getArea() returns Area
        if zones.size() < this.zoneNum
            error("getArea#1")
            return new Area()
        if zones.get(this.zoneNum).size() < this.p.getId()
            error("getArea#2")
            return new Area()

        return zones.get(this.zoneNum).get(this.p.getId())

public LinkedList<PlayerData> playerData = new LinkedList<PlayerData>

@initFirst function initPlayerData()
    // Init player level tracker
    for i = 0 to NUMBER_OF_PLAYERS-1
        PlayerData pd = new PlayerData(i, 0, 0, STARTING_LIFE)
        playerData.add(pd)

public function PlayerData.getLevel() returns Level
    return this.getLevels().get(this.levelNum)

public function PlayerData.getLevels() returns LinkedList<Level>
    return zoneLevels.get(this.zoneNum)

public function unit.getPlayerData() returns PlayerData
    return playerData.get(this.getOwner().getId())