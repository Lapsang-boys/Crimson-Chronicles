package PlayerData

import LinkedList
import ClosureForGroups

import Regions
import Leaderboard
import GameplayConstants

class Remover implements ForGroupCallback
    bool removePlayerUnits
    player p
    construct(bool removePlayerUnits, player p)
        this.removePlayerUnits = removePlayerUnits
        this.p = p

    function callback(unit u)
        if removePlayerUnits
            u.remove()
        else if not u.getOwner() == p
            u.remove()

public class PlayerData
    int playerNum
    player p
    int level
    int area
    int life
    int mobsRemoved
    unit builder
    bool levelInProgress
    int difficulty

    construct(int playerNum, int level, int area, int life)
        this.playerNum = playerNum
        this.p = Player(playerNum)
        this.level = level
        this.area = area
        this.life = life
        this.mobsRemoved = 0
        this.levelInProgress = false
        this.difficulty = NORMAL

    function setBuilder(unit builder)
        this.builder = builder

    function cleanse(bool removePlayerUnits)
        rect r = rectFromIndex(playerArea.get(this.playerNum))
        Remover rem = new Remover(removePlayerUnits, this.p)
        forUnitsInRect(r, rem)

    function reset()
        this.levelInProgress = false
        this.area = 0
        this.life = STARTING_LIFE
        this.mobsRemoved = 0
        this.level = 0
        updateLife(this.p, this.life)

public LinkedList<PlayerData> playerData = new LinkedList<PlayerData>

init
    // Init player level tracker
    for i = 0 to NUMBER_OF_PLAYERS-1
        PlayerData pd = new PlayerData(i, 0, 0, STARTING_LIFE)
        playerData.add(pd)
