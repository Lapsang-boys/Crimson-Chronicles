package Levels

import LinkedList

import Squad
import GameplayConstants
import MobType
import Colorscheme


// ----------------------------------------------------------------------------
// ===================== Level class and methods ==============================
// ----------------------------------------------------------------------------
public tuple bonus(int bonus)

public class Level
    LinkedList<SquadMember> squadChain
    int bonus
    string levelName
    string desc
    LinkedList<MobType> types

    construct (LinkedList<SquadMember> squadChain, bonus b, string levelName, LinkedList<MobType> types, string desc)
        this.squadChain = squadChain
        this.bonus = b.bonus
        this.levelName = levelName
        this.desc = desc
        this.types = types

    function printShortInfo(player p)
        printTimedToPlayer(this.levelName, DISPLAY_TIME, p)
        for mt in this.types
            printTimedToPlayer(mobHint(mt), DISPLAY_TIME, p)
        printTimedToPlayer(" ", DISPLAY_TIME, p)

    function printInfo(player p)
        this.printShortInfo(p)
        if this.desc.length() > 0
            printTimedToPlayer(this.desc, DISPLAY_TIME, p)

public LinkedList<LinkedList<Level>> zoneLevels = new LinkedList<LinkedList<Level>>

public function levelInfoMsg(int num, string name) returns string
    return discreetColor.colorize("[") + textColor.colorize("Level ") + numberColor.colorize(I2S(num)) + discreetColor.colorize("]")
            + textColor.colorize(" - ") + nameColor.colorize(name)

public function loreMessage(string desc) returns string
    return textColor.colorize(desc)

