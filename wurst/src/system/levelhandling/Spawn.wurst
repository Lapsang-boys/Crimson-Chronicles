package Spawn

import Assets
import TimerUtils
import RegisterEvents
import HashMap
import SoundUtils

import Orders
import MobType
import Alexandria
import Squad
import Movement
import Regions
import Leaderboard
import GameplayConstants
import BuilderConstants
import Levels
import PlayerData
import TimedEffect
import MobList
import SaveLoad

class TimerData
	player p
	Level l
	int cur

	construct(player p, Level l)
		this.p = p
		this.l = l
		this.cur = 0

		
function spawnLevel()
	let builder = GetTriggerUnit()
	let p = builder.getOwner()

	// Don't allow builders to be building when sending a wave.
	//
	// This is to fix a save bug where a builder has a build queue, but the
	// buildings are not built yet making the save function save the low gold
	// value but not the unfinished towers.
	if not builder.getCurrentOrder() == 0 // Passive 
		and not builder.getCurrentOrder() == Orders.smart
		printTimedToPlayer("Please, finish building your towers before summoning the next level.", 3, p)
		return

	builder.removeAbility(ID_NEXT_LEVEL)
	builder.addAbility(ID_LEVEL_IN_PROGRESS)
	PlayerData pd = p.getPlayerData()
	playSummonLevelSound(pd)
	pd.levelInProgress = true

	let l = pd.getLevel()
	if l.types.contains(MobType.BOSS)
		save(p)
		
	TimerData lt = new TimerData(p, l)
	let t = getTimer()

	t.setData(lt castTo int)
	t.start(0.1, function summonMobs)
	l.printInfo(p)
	updateLevel(p, pd.levelNum)

function summonMobs()
	let t = GetExpiredTimer()
	let lt = t.getData() castTo TimerData
	let p = lt.p
	let pd = p.getPlayerData()
	let chain = lt.l.squadChain
	int num = chain.size()

	if lt.cur == num or (not pd.levelInProgress)
		t.release()
		return

	SquadMember single = chain.get(lt.cur)
	lt.cur = lt.cur + 1
	real duration
	if lt.cur == num
		duration = 0.1
	else
		duration = chain.get(lt.cur).si.getDuration()

	let path = pd.getArea().paths.get(single.sr.toIndex())
	let spawn = path.getSpawn()
	var pos = spawn.randomPoint()
	switch single.sp
		case SpawnPosition.LEFT
			pos = vec2(spawn.getLeftBot().x+128, spawn.getCenterY())
		case SpawnPosition.CENTER
			pos = spawn.getCenter()
		case SpawnPosition.RIGHT
			pos = vec2(spawn.getRightBot().x-128, spawn.getCenterY())
		case SpawnPosition.RANDOM
			pos = spawn.randomPoint()

	let u = createUnit(PLAYER_BROWN,
		single.u_ids.get(pd.difficulty),
		pos,
		pos.angleTo(path.getFirstNode().getCenter()))

	createTimedEffect(pos, 1., Abilities.aIilTarget)
	// Ten's place designates the path
	let cv = single.sr.toIndex()*10 + 1
	u.setUserData(cv)
	u.issuePointOrder("move", u.getNext())

	shelfMob(u)

	if mobMap.has(u.getHandleId())
		mobMap.get(u.getHandleId()).onSpawn()

	t.start(duration, function summonMobs)

function playSummonLevelSound(PlayerData pd)
	let sp = SUMMON_LEVEL_SOUND_MAP.get(pd.builder.getTypeId())
	SoundDefinition sd = new SoundDefinition(sp, false)
	sd.playForPlayer(pd.p)

@initThird function initSpawn()
	registerSpellEffectEvent(ID_NEXT_LEVEL, function spawnLevel)

