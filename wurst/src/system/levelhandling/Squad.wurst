package Squad

import LinkedList

public constant DEFAULT_SPAWN_INTERVAL = spawnInterval(1.80, 1.80)
public tuple spawnInterval(real low, real high)
public function spawnInterval.getDuration() returns real
    return GetRandomReal(this.low, this.high)

public enum SpawnRegion
    FIRST
    SECOND
    THIRD
    SPUTNIK
    RANDOM

public function SpawnRegion.toIndex() returns int
    switch this
        case FIRST
            return 0
        case SECOND
            return 1
        case THIRD
            return 2
        case SPUTNIK
            return 3
        case RANDOM
            return 4
        // Not even sputnik.
        default
            return 0

public enum SpawnPosition
    LEFT
    CENTER
    RIGHT
    RANDOM

public class SquadMember
    LinkedList<int> u_ids
    int num
    SpawnPosition sp
    spawnInterval si
    SpawnRegion sr

    construct(LinkedList<int> u_ids, spawnNum sn, SpawnPosition sp, spawnInterval si, SpawnRegion sr)
        this.u_ids = u_ids
        this.num = sn.spawnNum
        this.sp = sp
        this.si = si
        this.sr = sr

public function LinkedList<SquadMember>.toSquadChain() returns LinkedList<SquadMember>
    LinkedList<SquadMember> squadChain = new LinkedList<SquadMember>
    for sm in this
        for i = 0 to sm.num-1
            SquadMember single = new SquadMember(sm.u_ids, spawnNum(1), sm.sp, sm.si, sm.sr)
            squadChain.add(single)
    return squadChain

public tuple spawnNum(int spawnNum)

public function toSquad(LinkedList<int> u_ids, spawnNum sn, spawnInterval si, SpawnRegion sr) returns LinkedList<SquadMember>
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, sn, SpawnPosition.RANDOM, si, sr)).toSquadChain()

public function toDefaultSquad(LinkedList<int> u_ids, spawnNum sn) returns LinkedList<SquadMember>
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, sn, SpawnPosition.RANDOM, DEFAULT_SPAWN_INTERVAL, SpawnRegion.FIRST)).toSquadChain()
