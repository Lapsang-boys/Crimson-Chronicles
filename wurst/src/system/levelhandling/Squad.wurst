package Squad

import LinkedList

public constant DEFAULT_SPAWN_INTERVAL = spawnInterval(1.75, 1.75)
public tuple spawnInterval(real low, real high)
public function spawnInterval.getDuration() returns real
    return GetRandomReal(this.low, this.high)

public enum SpawnPosition
    LEFT
    CENTER
    RIGHT
    RANDOM

public class SquadMember
    LinkedList<int> u_ids
    int num
    SpawnPosition sp
    spawnInterval si

    construct(LinkedList<int> u_ids, int num, SpawnPosition sp, spawnInterval si)
        this.u_ids = u_ids
        this.num = num
        this.sp = sp
        this.si = si

public function LinkedList<SquadMember>.toSquadChain() returns LinkedList<SquadMember>
    LinkedList<SquadMember> squadChain = new LinkedList<SquadMember>
    for sm in this
        for i = 0 to sm.num-1
            SquadMember single = new SquadMember(sm.u_ids, 1, sm.sp, sm.si)
            squadChain.add(single)
    return squadChain

public function toSquad(LinkedList<int> u_ids, int num, real low, real high) returns LinkedList<SquadMember>
    let si = spawnInterval(low, high)
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, num, SpawnPosition.RANDOM, si)).toSquadChain()

public function toDefaultSquad(LinkedList<int> u_ids, int num) returns LinkedList<SquadMember>
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, num, SpawnPosition.RANDOM, DEFAULT_SPAWN_INTERVAL)).toSquadChain()

public function toLeftSquad(LinkedList<int> u_ids, int num) returns LinkedList<SquadMember>
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, num, SpawnPosition.LEFT, DEFAULT_SPAWN_INTERVAL)).toSquadChain()

public function toCenterSquad(LinkedList<int> u_ids, int num) returns LinkedList<SquadMember>
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, num, SpawnPosition.CENTER, DEFAULT_SPAWN_INTERVAL)).toSquadChain()

public function toRightSquad(LinkedList<int> u_ids, int num) returns LinkedList<SquadMember>
    return new LinkedList<SquadMember>..add(new SquadMember(u_ids, num, SpawnPosition.RIGHT, DEFAULT_SPAWN_INTERVAL)).toSquadChain()
