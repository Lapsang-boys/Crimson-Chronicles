package AgileMob

import TimerUtils
import Assets

import Alexandria
import Squad

public class AgileBase extends MobWrapper implements Enterer
    real speedBoost
    real origSpeed
    real duration
    timer t
    effect e
    construct(unit u, real origSpeed, real speedBoost, real duration)
        super(u)
        this.origSpeed = origSpeed
        this.speedBoost = speedBoost
        this.duration = duration
        this.t = getTimer()

    override function onEnter()
        super.u.setMoveSpeed(this.origSpeed + this.speedBoost)
        this.e = super.u.addEffect(Abilities.tornado_Target, "origin")

        t.setData(this castTo int)
        t.start(duration, () -> begin
            timer t = GetExpiredTimer()
            AgileBase am = t.getData() castTo AgileBase
            am.getUnit().setMoveSpeed(am.origSpeed)
            am.e.destr()
        end)

    ondestroy
        this.t.release()

// Squad hook
public class Agile implements Hooker
    real origSpeed
    real speedBoost
    real duration
    construct(real origSpeed, real speedBoost, real duration)
        this.origSpeed = origSpeed
        this.speedBoost = speedBoost
        this.duration = duration
    function hook(unit u)
        new AgileBase(u, this.origSpeed, this.speedBoost, this.duration)