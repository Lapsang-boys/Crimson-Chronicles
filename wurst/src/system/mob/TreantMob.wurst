package TreantMob

import InstantDummyCaster
import Assets
import BuffObjEditing
import ChannelAbilityPreset
import ClosureForGroups

import Tooltip
import GameplayConstants
import ID2S
import Alexandria
import Squad
import TimedEffect

constant DURATION = 2.
constant AS_SLOW = 0.5

public class TreantBase extends MobWrapper implements Deather
    unit u
    int abilityId
    real duration
    real radius
    string fxDeathPath
    construct(unit u, int abilityId, real duration, real radius, string fxDeathPath)
        super(u)
        this.u = u
        this.abilityId = abilityId
        this.radius = radius
        this.duration = duration
        this.fxDeathPath = fxDeathPath

    override function onDeath()
        createTimedEffect(this.u.getPos(), 1.5, this.fxDeathPath)
        SmogTarget st = new SmogTarget(this.u)
        forUnitsInRange(this.u.getPos(), this.radius, st)



// Squad hook
public class Treant implements Hooker
    int abilityId
    real duration
    real radius
    string fxDeathPath

    construct(int abilityId, real duration, real radius, string fxDeathPath)
        this.abilityId = abilityId
        this.duration = duration
        this.radius = radius
        this.fxDeathPath = fxDeathPath
    function hook(unit u)
        new TreantBase(u, this.abilityId, this.duration, this.radius, this.fxDeathPath)

public constant ID_FEL_SMOG_BUFF_BASE = 'Blcb'
public constant ID_FEL_SMOG_BUFF = 'Blzq'
public constant ID_FEL_SMOG = 'Asdg'

// ##################################################################
// ###################### Group callback ############################
// ##################################################################

class SmogTarget implements ForGroupCallback
    unit treant

    construct(unit treant)
        this.treant = treant

    function callback(unit u)
        if u.getOwner() != PLAYER_BROWN
            InstantDummyCaster.castTarget(this.treant.getOwner(), ID_FEL_SMOG, 1, "cripple", u, this.treant.getPos())

// ##################################################################
// ###################### Ability objects ###########################
// ##################################################################

class FelSmogBuff extends BuffDefinition
    construct(int newBuffId, int origBuffId)
        super(newBuffId, origBuffId)

        setIcon(Icons.bTNCarrionSwarm)
        setTarget(1, Units.plagueCloudTarget)

        setTargetAttachmentPoint0(1, "origin")
        setMissileArt(1, "")

        setTooltipNormal(1, tooltipBuffName("Fel smog ###", false))
        setTooltipNormalExtended(1, tooltipBuffText("This unit is poisoned. ###"))

class FelSmog extends AbilityDefinitionCripple
    construct(int id)
        super(id)
        setAttackSpeedReduction(1, AS_SLOW)
        setDurationNormal(1, DURATION)

        // Dummy requirements
        setManaCost(1, 0)
        setCastRange(1, 92083)
        setCooldown(1, 0)
        setCastingTime(1, 0)
        setAnimationNames("")

        setHeroAbility(false)
        setRequirements("")
        setArtTarget("")
        setRequirements("")
        setLevels(1)
        setTargetsAllowed(1, "enemies,structure")
        setBuffs(1, ID2S(ID_FEL_SMOG_BUFF))

@compiletime function generateFelSmog()
    new FelSmog(ID_FEL_SMOG)
    new FelSmogBuff(ID_FEL_SMOG_BUFF, ID_FEL_SMOG_BUFF_BASE)