package DemonMob

import ClosureForGroups

import GameplayConstants
import Alexandria
import TimedEffect

class DemonGroup implements ForGroupCallback
    real amount
    string fxPath
    construct(real amount, string fxPath)
        this.amount = amount
        this.fxPath = fxPath
    override function callback(unit u)
        if u.getOwner() != PLAYER_BROWN
            return

        u.setHP(u.getHP() + this.amount)
        u.attatchTimedEffect(0.25, this.fxPath, "origin")

public class DemonMob extends MobWrapper implements Deather
    real percent
    real radius
    unit u
    string fxDeathPath
    string fxHealPath
    construct(unit u, real percent, real radius, string fxDeathPath, string fxHealPath)
        super(u)
        this.percent = percent
        this.radius = radius
        this.u = u
        this.fxDeathPath = fxDeathPath
        this.fxHealPath = fxHealPath

    override function onDeath(player p)
        createTimedEffect(this.u.getPos(), 1.5, this.fxDeathPath)
        DemonGroup dg = new DemonGroup(this.u.getMaxHP()*this.percent, this.fxHealPath)
        forUnitsInRange(this.u.getPos(), this.radius, dg)
