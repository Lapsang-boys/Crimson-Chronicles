package NecroMob

import ClosureTimers
import Abilities

import StratholmeMobList
import TimedEffect
import Alexandria
import Movement
import PlayerData
import LevelHandler

constant sekeltonSpawnTime = 2.3

public class NecroMob extends MobWrapper implements Enterer
    real delay
    real damage
    int skeletons

    construct(unit u, real delay, real damage, int skeletons)
        super(u)
        this.delay = delay
        this.damage = damage
        this.skeletons = skeletons

    override function onEnter()
        this.u.setAnimation("stand channel")
        this.u.pause()
        effect e = createTimedEffect(this.u.getPos(), this.delay, Abilities.vampiricAura)
            ..setScale(2)
            ..setColor(0, 255, 0)
        // Hide effect by height because death is super slow
        doAfter(this.delay + sekeltonSpawnTime) ->
            e.setZ(-100)

        let pd = playerData.get(u.getPlayerN())

        for i = 1 to this.skeletons
            doAfter(i*this.delay/this.skeletons) ->
                var offset = angle(PI/16 + i*2*PI/this.skeletons).direction().setLength(128)
                var spawnPos = this.u.getPos().add(offset.x, offset.y)
                createTimedEffect(spawnPos, 2, Abilities.raiseSkeleton)

                let skeleton = createUnit(
                    this.u.getOwner(),
                    ID_MOB_SKELETON.get(pd.difficulty),
                    spawnPos,
                    this.u.getPos().angleTo(this.u.getNext()))

                new SkeletonMob(skeleton, this)
                registerMobCreation(pd.p)

                // Make skeleton transparent.
                skeleton.setVertexColor(colorA(178, 178, 178, 180))
                skeleton.setUserData(this.u.getUserData())
                skeleton.pause()
                skeleton.setAnimation("birth")
                doAfter(sekeltonSpawnTime) ->
                    skeleton.unpause()
                    vec2 pos = skeleton.getNext()
                    skeleton.issuePointOrder("move", pos)

        doAfter(1.2*this.delay) ->
            this.u.unpause()


public class SkeletonMob extends MobWrapper implements Deather
    NecroMob nm

    construct(unit u, NecroMob nm)
        super(u)
        this.nm = nm

    override function onDeath(player p)
        let pd = p.getPlayerData()

        // The skeleton might have lived longer than the necromancer.
        if nm == null
            return

        vec2 pos = nm.u.getPos()
        createTimedEffect(pos, 2, Abilities.deathCoilSpecialArt)
        pd.builder.damageTarget(nm.u, nm.damage)
