package PowerFaithMob

import TimerUtils
import Assets
import ClosureForGroups

import Alexandria
import GameplayConstants
import TimedEffect

public class PowerFaithMob extends MobWrapper implements Enterer
    real healAmount
    real healRadius
    real duration
    int itterations
    int i
    timer t
    effect e
    string healFx
    string healAnimationName
    construct(unit u, real healAmount, real healRadius, real duration, int itterations, string healFx, string healAnimationName)
        super(u)
        this.healAmount = healAmount
        this.healRadius = healRadius
        this.itterations = itterations
        this.duration = duration
        this.healFx = healFx
        this.healAnimationName = healAnimationName

        this.i = 0
        this.t = getTimer()

    override function onEnter()
        super.u.setInvulnerable(true)
        super.u.pause()
        super.u.setAnimation(this.healAnimationName)
        this.e = super.u.addEffect(Abilities.divineShieldTarget, "origin")

        this.t.setData(this castTo int)
        this.t.start(duration, function periodicHeal)

    ondestroy
        this.t.release()

function periodicHeal()
    timer t = GetExpiredTimer()
    PowerFaithMob pf = t.getData() castTo PowerFaithMob

    HealTarget ht = new HealTarget(pf)
    forUnitsInRange(pf.getUnit().getPos(), pf.healRadius, ht)
    pf.i++

    if pf.i <= pf.itterations
        pf.getUnit().setAnimation(pf.healAnimationName)
        t.start(pf.duration, function periodicHeal)
    else
        pf.getUnit().setInvulnerable(false)
        pf.getUnit().unpause()
        pf.e.destr()
        pf.i = 0

// ##################################################################
// ###################### Group callback ############################
// ##################################################################

class HealTarget implements ForGroupCallback
    PowerFaithMob priest

    construct(PowerFaithMob priest)
        this.priest = priest

    override function callback(unit u)
        if u.getOwner() == PLAYER_BROWN
            u.attatchTimedEffect(1., this.priest.healFx, "origin")
            u.setHP(u.getHP() + this.priest.healAmount)
