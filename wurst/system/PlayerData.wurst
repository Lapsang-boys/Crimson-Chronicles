package PlayerData

import LinkedList
import SoundUtils

import Levels
import Leaderboard
import Constants

public class PlayerData
    int playerNum
    int level
    int area
    int life
    int mobsRemoved
    unit builder

    construct(int playerNum, int level, int area, int life)
        this.playerNum = playerNum
        this.level = level
        this.area = area
        this.life = life
        this.mobsRemoved = 0

    function setBuilder(unit builder)
        this.builder = builder

    function loseLife(int i)
        this.life -= i
        if this.life >= 1
            if i > 1
                print("You lost " + I2S(i) + " lives.")
            else
                print("You lost " + I2S(i) + " life.")
        else
            loseGame()

    function loseGame()
        print("You lost.")

    function registerMobLeave()
        this.mobsRemoved++
        Level l = levels.get(this.level)
        player p = Player(this.playerNum)
        this.life--
        updateLife(p, this.life)
        if this.mobsRemoved == levels.get(this.level).num
            print("Level finished!")
            this.builder.removeAbility(ID_LEVEL_IN_PROGRESS)
            this.builder.addAbility(ID_NEXT_LEVEL)
            p.addGold(l.bonus)

            this.level++
            this.mobsRemoved = 0
            
            SoundDefinition sd = new SoundDefinition("Sound\\Interface\\QuestCompleted.wav", false)
            sd.playForPlayer(p)
            
            // might change area later.

public LinkedList<PlayerData> playerData = new LinkedList<PlayerData>

init
    // Init player level tracker
    for i = 0 to NUMBER_OF_PLAYERS-1
        PlayerData pd = new PlayerData(i, 0, 0, STARTING_LIFE)
        playerData.add(pd)