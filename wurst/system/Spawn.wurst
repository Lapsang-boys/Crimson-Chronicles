package Spawn

import TimerUtils
import OnUnitEnterLeave
import RegisterEvents

import Movement
import Constants
import Levels
import PlayerData

class TimerData
	player p
	Level l
	int cur

	construct(player p, Level l)
		this.p = p
		this.l = l
		this.cur = 0

function spawnLevel()
	let builder = GetTriggerUnit()
	let p = builder.getOwner()
	builder.removeAbility(ID_NEXT_LEVEL)
	builder.addAbility(ID_LEVEL_IN_PROGRESS)
	PlayerData pd = playerData.get(p.getId())

	int levelIndex = pd.level
	let l = levels.get(levelIndex)

	TimerData lt = new TimerData(p, l)
	let t = getTimer()
	
	t.setData(lt castTo int)
	t.start(l.getDuration(), function summonMobs)
	l.printInfo()
	
function summonMobs()
	let t = GetExpiredTimer()
	let lt = t.getData() castTo TimerData
	if lt.cur == lt.l.num
		t.release()
		return

	lt.cur = lt.cur + 1
	let p = lt.p
	let pos = getSpawn(p).randomPoint()	
	let u = createUnit(PLAYER_BROWN, lt.l.uID, pos, angle(PI/2))
	u.setUserData(1)
	u.issuePointOrder("move", getNext(u))
	
	t.start(lt.l.getDuration(), function summonMobs)

init
	let pos = getSpawn(Player(0)).randomPoint()
	let u = createUnit(Player(0), ID_BUILDER, pos, angle(PI/2))
	playerData.get(0).setBuilder(u)
	registerSpellEffectEvent(ID_NEXT_LEVEL, function spawnLevel)