package Spawn

import TimerUtils
import OnUnitEnterLeave
import RegisterEvents

import Movement
import Leaderboard
import Constants
import Levels
import PlayerData

let SPAWN_FX = "Abilities\\Spells\\Items\\AIil\\AIilTarget.mdl"

class TimerData
	player p
	Level l
	int cur

	construct(player p, Level l)
		this.p = p
		this.l = l
		this.cur = 0

function spawnLevel()
	let builder = GetTriggerUnit()
	let p = builder.getOwner()
	builder.removeAbility(ID_NEXT_LEVEL)
	builder.addAbility(ID_LEVEL_IN_PROGRESS)
	PlayerData pd = playerData.get(p.getId())
	pd.levelInProgress = true

	int levelIndex = pd.level
	let l = levels.get(levelIndex)

	TimerData lt = new TimerData(p, l)
	let t = getTimer()

	t.setData(lt castTo int)
	t.start(l.getDuration(), function summonMobs)
	l.printInfo(p)
	updateLevel(p, pd.level+1)

function summonMobs()
	let t = GetExpiredTimer()
	let lt = t.getData() castTo TimerData
	let p = lt.p
	if lt.cur == lt.l.num or (not playerData.get(p.getId()).levelInProgress)
		t.release()
		return

	lt.cur = lt.cur + 1
	let pos = getSpawn(p).randomPoint()
	let u = createUnit(PLAYER_BROWN, lt.l.uID, pos, angle(PI/2))
	flashEffect(SPAWN_FX, pos)

	u.setUserData(1)
	u.issuePointOrder("move", getNext(u))

	t.start(lt.l.getDuration(), function summonMobs)

init
	registerSpellEffectEvent(ID_NEXT_LEVEL, function spawnLevel)